buildscript {
    dependencies {
        classpath 'org.postgresql:postgresql:42.2.9'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.30'
    id 'org.flywaydb.flyway' version '6.2.1'
    id 'com.dorongold.task-tree' version '1.5'
}

ext {
    kotlin_version='1.3.30'
    vertx_version='3.8.1'
    slf4j_version='1.7.30'
    graphql_version='13.0'
}

// IP of docker machine. Defaults to localhost if environment variable is not set.
def docker_ip = System.getenv().getOrDefault("DOCKER_IP", "localhost")

// Helpful function that determines if this machine is running on Windows or not.
def isWindows() {
    return System.env['OS'].contains('Windows')
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.vertx:vertx-core:$vertx_version"
    implementation "io.vertx:vertx-lang-kotlin:$vertx_version"
    implementation "io.vertx:vertx-web:$vertx_version"
    implementation "io.vertx:vertx-mysql-postgresql-client:$vertx_version"
    implementation "io.vertx:vertx-lang-kotlin-coroutines:$vertx_version"
    implementation "io.vertx:vertx-config:$vertx_version"
    implementation "org.slf4j:slf4j-api:$slf4j_version"
    implementation "com.graphql-java:graphql-java:$graphql_version"
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

flyway {
    url = "jdbc:postgresql://${docker_ip}/ruined"
    user = 'admin'
    password = 'admin'
    connectRetries = 5
}

task preprocessConfigs(type: Copy) {
    group 'ruined'
    from('conf.templates') {
        include '**/*.json'
    }
    into 'conf'
    expand(DOCKER_IP: docker_ip)
}

task refreshDB() {
    group 'ruined.helpers'
    description 'Stops db container if it is running, removes it and starts it again. Flyway migration is then applied.'
    doLast {
        exec { commandLine 'docker-compose', 'stop', 'db' }
        exec { commandLine 'docker-compose', 'rm', '-f', 'db' }
        exec { commandLine 'docker-compose', 'up', '-d', 'db' }
    }
}

task startApp(type: JavaExec) {
    group 'ruined'
    description 'Runs the application in the foreground. Redploys code when classes are rebuilt.'
    classpath sourceSets.main.runtimeClasspath
    main 'io.vertx.core.Launcher'
    args 'run', 'ruined.RuinedVerticle',
            '--redeploy="build/classes/kotlin/**/*.class"',
            '--launcher-class="io.vertx.core.Launcher"',
            '--java-opts', '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
}

task startContainers() {
    group = 'ruined'
    description 'Starts containers specified in docker-compose.yml'
    doLast {
        exec {
            commandLine 'docker-compose', 'up', '-d'
        }
    }
}

task updateDB() {
    group 'ruined'
    description 'Clears current database and runs migration'
}

task stopContainers(type: Exec) {
    group = 'ruined'
    description 'Stops containers specified in docker-compose.yml'
    commandLine 'docker-compose', 'stop'
}

task stopApp(type: Exec) {
    group = 'ruined'
    description 'Kills off any orphaned verticles that might still be running. This is necessary when the task startApp has been forcibly closed.'
    if(isWindows()) {
        commandLine 'WMIC', 'PROCESS', 'WHERE', "CommandLine LIKE '%vertx.id=%'", 'CALL', 'TERMINATE'
    }
    else {
        // Missing *Nix implementation
    }
}

compileKotlin.kotlinOptions.jvmTarget = '1.8'

flywayMigrate.dependsOn(':startContainers')
updateDB.dependsOn(':refreshDB', ':flywayMigrate')
startApp.dependsOn(':startContainers', ':preprocessConfigs')
flywayMigrate.mustRunAfter(':refreshDB')